# Maintainer: Mort Yao
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

### NOTE: This git package must be built on an older system with automake-1.11 and librsvg-2.44
### If you intend to build on a newer system, use the distributed source of 2.8.22 and overwrite the code
### with changes on git, and change librsvg/librsvg-features.h to librsvg/rsvg-features.h

_pkgname=gimp
pkgname=my-$_pkgname-git
_pkgver=2.8.22
pkgver=2.8.22.r32515.4c27ddb859
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=('pygtk' 'lcms' 'libxpm' 'libwmf' 'libxmu' 'librsvg' 'libmng' 'dbus-glib'
         'libexif' 'gegl02' 'jasper' 'desktop-file-utils' 'hicolor-icon-theme' 'babl'
         'openexr' 'libgudev')
makedepends=('intltool' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl' 'ghostscript')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support')
options=('!makeflags')
provides=('gimp')
conflicts=('gimp-devel' 'gimp')
replaces=('gimp-devel' 'gimp')
source=("${_pkgname}::git+https://github.com/soimort/${_pkgname}.git"
        linux.gpl
        uri-backend-libcurl.patch)
sha256sums=(SKIP
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3'
            '0708a971455fba6406bfa190652797c6620c789a9d21012c57249d2d4878bd55')

pkgver() {
  cd ${_pkgname}
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${_pkgname}
  PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console --enable-gimp-remote \
    --enable-python --with-gif-compression=lzw --with-libcurl \
    --without-aa --without-hal --without-gvfs --without-gnomevfs
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}" install
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}
