# Maintainer: Mort Yao
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

_pkgname=caja
pkgname=mi-$_pkgname-git
pkgver=1.26.0.r44.g1df5fde6
pkgrel=1
pkgdesc="File manager for the MATE desktop"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('exempi' 'gvfs' 'libexif' 'libsm' 'mate-desktop>=1.22' 'libnotify')
makedepends=('gobject-introspection' 'intltool')
optdepends=('gstreamer: automatic media playback when mouseover')
groups=('mate')
provides=('caja')
conflicts=('caja' 'caja-gtk3')
replaces=('caja' 'caja-gtk3')
source=("${_pkgname}::git+https://github.com/soimort/mi-caja.git")
sha1sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//'
}

build() {
  cd $_pkgname
  git submodule init
  git submodule update --remote --recursive
  ./autogen.sh --prefix=/usr
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$_pkgname \
    --enable-introspection \
    --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $_pkgname
  make DESTDIR="${pkgdir}" install

  # Remove D-Bus activation file to avoid conflict with nautilus-desktop
  rm -r "$pkgdir/usr/share/dbus-1/"
}
